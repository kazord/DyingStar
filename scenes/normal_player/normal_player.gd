extends CharacterBody3D
class_name Player

@warning_ignore("unused_signal")
signal client_action_requested(datas: Dictionary)

@onready var camera = $CameraPivot/Camera3D

@onready var labelx: Label = $UserInterface/LabelXValue
@onready var labely: Label = $UserInterface/LabelYValue
@onready var labelz: Label = $UserInterface/LabelZValue
@onready var labelPlayerName: Label3D = %LabelPlayerName
@onready var astronaut: Node3D = $Placeholder_Collider/Astronaut
@onready var interact_ray: RayCast3D = $CameraPivot/Camera3D/InteractRay
@onready var interact_label: Label = $UserInterface/InteractLabel
@onready var camera_pivot: Node3D = $CameraPivot

@onready var direct_chat: GlobalChat = $UserInterface/DirectChat

@onready var box4m: PackedScene = preload("res://scenes/props/testbox/box_4m.tscn")
@onready var box50m: PackedScene = preload("res://scenes/props/testbox/box_50cm.tscn")
@onready var isInsideBox4m: bool = false

@onready var game_is_paused: bool = false

@export_group("Controls map names")
@export var MOVE_FORWARD: String = "move_forward"
@export var MOVE_BACK: String = "move_back"
@export var MOVE_LEFT: String = "move_left"
@export var MOVE_RIGHT: String = "move_right"
@export var JUMP: String = "jump"
@export var CROUCH: String = "crouch"
@export var SPRINT: String = "sprint"
@export var PAUSE: String = "pause"

@export_group("Customizable player stats")
@export var walk_back_speed: float = 1.5
@export var walk_speed: float = 2.5
@export var player_thruster_force = 10
@export var sprint_speed: float = 5.0
@export var crouch_speed: float = 1.5
@export var jump_height: float = 1.0
@export var acceleration: float = 10.0
@export var arm_length: float = 0.5
@export var regular_climb_speed: float = 6.0
@export var fast_climb_speed: float = 8.0
@export_range(0.0, 1.0) var view_bobbing_amount: float = 1.0
@export_range(1.0, 10.0) var camera_sensitivity: float = 2.0
@export_range(0.0, 0.5) var camera_start_deadzone: float = .2
@export_range(0.0, 0.5) var camera_end_deadzone: float = .1

var player_display_name: String = ""

var input_direction: Vector2
var movement_strength: float
var mouse_motion: Vector2
var is_jumping: bool = false

var spawn_position: Vector3 = Vector3.ZERO
var spawn_up: Vector3 = Vector3.UP

var can_interact: bool = false

@export var gravity = 0.0

var gravity_parents: Array[Area3D]

# to disable player input when piloting vehicule/ship
var active = true

func _enter_tree() -> void:
	pass

func _ready() -> void:
	if not is_multiplayer_authority():
		return
	
	global_position = spawn_position
	look_at(global_transform.origin + Vector3.FORWARD, spawn_up)
	
	Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
	camera.current = true
	# hide player name label for me only
	labelPlayerName.visible = false
	astronaut.visible = false
	interact_label.hide()
	connect_area_detect()

func connect_area_detect():
	$AreaDetector.area_entered.connect(_on_area_detector_area_entered)
	$AreaDetector.area_exited.connect(_on_area_detector_area_exited)

func _unhandled_input(event: InputEvent) -> void:
	if not is_multiplayer_authority(): return
	if !active: return
	
	if not should_listen_input(): return

	if GameOrchestrator.current_state != GameOrchestrator.GAME_STATES.PAUSE_MENU:
		if event.is_action_pressed(PAUSE):
			GameOrchestrator.change_game_state(GameOrchestrator.GAME_STATES.PAUSE_MENU)
		
		if event.is_action_pressed(JUMP) and is_on_floor():
			is_jumping = true
		
		if event is InputEventMouseMotion and Input.mouse_mode == Input.MOUSE_MODE_CAPTURED:
			mouse_motion = -event.relative * 0.001
		
		if event.is_action_pressed("spawn_50cmbox"):
			var box_spawn_position: Vector3 = global_position + (-global_basis.z * 1.5) + global_basis.y * 2.0
			emit_signal("client_action_requested", {"action": "spawn", "entity": "box50cm", "spawn_position": box_spawn_position})
		
		if event.is_action_pressed("spawn_4mbox"):
			var box_spawn_position: Vector3 = global_position + (-global_basis.z * 3.0) + global_basis.y * 6.0
			
			var player_up = global_transform.basis.y.normalized()
			var to_player = (global_transform.origin - box_spawn_position)
			to_player -= to_player.dot(player_up) * player_up
			to_player = to_player.normalized()
			var box_basis: Basis = Basis.looking_at(to_player, player_up)
			var box_spawn_rotation = box_basis.get_euler()
			
			emit_signal("client_action_requested", {"action": "spawn", "entity": "box4m", "spawn_position": box_spawn_position, "spawn_rotation": box_spawn_rotation})
		
		if Input.is_action_just_pressed("ext_cam"):
			if $ExtCamera3D.current:
				camera.make_current()
				astronaut.visible = false
			else: 
				astronaut.visible = true
				$ExtCamera3D.make_current()
	else:
		if event is InputEventMouseButton:
			GameOrchestrator.change_game_state(GameOrchestrator.GAME_STATES.PLAYING)

func _process(_delta: float) -> void:
	if not is_multiplayer_authority(): return
	if !active:
		interact_label.hide()
		return
	
	if GameOrchestrator.current_state != GameOrchestrator.GAME_STATES.PAUSE_MENU:
		_handle_camera_motion()
			
		interact_label.hide()
		can_interact = false
		if interact_ray.is_colliding():
			var collider = interact_ray.get_collider()
			if collider.has_method("interact"):
				interact_label.text = collider.label
				interact_label.show()
				can_interact = true
				if Input.is_action_just_pressed("interact"):
					collider.interact(self)
					interact_label.hide()

func _physics_process(delta: float) -> void:
	if not is_multiplayer_authority(): return
	if !active: return
	
	var dir_vect = Vector3.ZERO
	#var jump = null
	var sprint = null
	if GameOrchestrator.current_state != GameOrchestrator.GAME_STATES.PAUSE_MENU:
		if not direct_chat.visible:
			dir_vect = Input.get_vector(MOVE_LEFT, MOVE_RIGHT, MOVE_FORWARD, MOVE_BACK)
	
		sprint = Input.is_action_pressed(SPRINT)
	
	if dir_vect and should_listen_input():
		input_direction = dir_vect
	else:
		input_direction = Vector2.ZERO
	
	var parent_gravity_area: Area3D = gravity_parents.back() if not gravity_parents.is_empty() else null
	
	if parent_gravity_area:
		
		if parent_gravity_area.gravity_point:
			var space_state = get_world_3d().direct_space_state
			var param = PhysicsRayQueryParameters3D.new()
			param.from = global_position
			param.to = parent_gravity_area.global_position
			var result = space_state.intersect_ray(param)
			if result:
				up_direction = result.normal
		else:
			up_direction = parent_gravity_area.global_basis.y
		
		gravity = parent_gravity_area.gravity
		motion_mode = CharacterBody3D.MOTION_MODE_GROUNDED
	else:
		# 0g movement
		gravity = 0.0
		camera_pivot.rotation.x = 0
		motion_mode = CharacterBody3D.MOTION_MODE_FLOATING
		var dir = Vector3(input_direction.x, 0, input_direction.y)
		
		velocity += global_basis * dir * player_thruster_force * delta
		velocity *= 0.98
	
	var move_direction = (global_transform.basis * Vector3(input_direction.x, 0, input_direction.y)).normalized()
	
	if gravity > 0:
		orient_player()
	
	var speed = sprint_speed if sprint else walk_speed
	
	if is_on_floor():
		if input_direction and should_listen_input():
			velocity = move_direction * speed
		else:
			velocity = velocity.move_toward(Vector3.ZERO, speed)
	else:
		# "air" movement
		if input_direction:
			velocity += move_direction * speed * delta
			
	if is_on_floor() and is_jumping and should_listen_input():
		velocity += up_direction * jump_height * gravity
		is_jumping = false
	# Add the gravity.
	elif not is_on_floor():
		velocity -= up_direction * gravity * 2.0 * delta
		
	move_and_slide()
	
	labelx.text = str("%0.2f" % global_position[0])
	labely.text = str("%0.2f" % global_position[1])
	labelz.text = str("%0.2f" % global_position[2])

func should_listen_input() -> bool:
	return not (GlobalChat.is_shown || MenuConfig.is_shown)

func _handle_camera_motion():
	if gravity == 0:
		camera_pivot.rotation.x = 0
		rotate_object_local(Vector3.UP, mouse_motion.x  * camera_sensitivity)
		rotate_object_local(Vector3.RIGHT, mouse_motion.y  * camera_sensitivity)
	else:
		orient_player()
		global_basis = global_basis.rotated(global_basis.y, mouse_motion.x * camera_sensitivity)
		camera_pivot.rotate_object_local(Vector3.RIGHT, mouse_motion.y  * camera_sensitivity)
		camera_pivot.rotation_degrees.x = clamp(camera_pivot.rotation_degrees.x, -80, 80)
	mouse_motion = Vector2.ZERO

func orient_player():
	global_transform = global_transform.interpolate_with(Globals.align_with_y(global_transform, up_direction), 0.3)

func set_player_name(player_name):
	labelPlayerName.text = str(player_name)
	
func get_player_name():
	print(labelPlayerName.text)

func _on_area_detector_area_entered(area: Area3D) -> void:
	if area.is_in_group("gravity"):
		gravity_parents.push_back(area)
		prints("player entered gravity area", area)

func _on_area_detector_area_exited(area: Area3D) -> void:
	if area.is_in_group("gravity"):
		if gravity_parents.has(area):
			prints("player left gravity area", area)
			gravity_parents.erase(area)
